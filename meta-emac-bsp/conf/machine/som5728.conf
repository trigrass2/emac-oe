#@TYPE: Machine
#@NAME: SOM-5728
#@DESCRIPTION: Machine configuration for the SOM-5728

MACHINEOVERRIDES =. "dra7xx-evm:"
include conf/machine/dra7xx-evm.conf

KERNEL_DEVICETREE = ""

UBOOT_MACHINE = "dra7xx_evm_config"

MACHINE_FEATURES:append = " serial ext3 ext4 can i2c spi"

# If x11 is desired, use software rendering
# instead of out-of-tree SGX and RGX drivers
DISTRO_FEATURES:remove = "x11"
DISTRO_FEATURES:append = " wayland pam opengl"

MACHINE_FEATURES:remove = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','gpu','',d)}"
PREFERRED_PROVIDER_virtual/egl = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','mesa','ti-sgx-ddk-um',d)}"
PREFERRED_PROVIDER_virtual/libgles1 = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','mesa','ti-sgx-ddk-um',d)}"
PREFERRED_PROVIDER_virtual/libgles2 = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','mesa','ti-sgx-ddk-um',d)}"
PREFERRED_PROVIDER_virtual/libgbm = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','mesa','ti-sgx-ddk-um',d)}"
PREFERRED_PROVIDER_virtual/gpudriver = "${@bb.utils.contains( \
    'DISTRO_FEATURES','x11','','ti-sgx-ddk-km',d)}"

# Taken from meta-arago
# Prefer OpenSSL over GnuTLS for curl due to TSU exemptions
PACKAGECONFIG:pn-curl = "${@bb.utils.contains("DISTRO_FEATURES", "ipv6", "ipv6", "", d)} proxy openssl verbose zlib"
PACKAGECONFIG:pn-glmark2 = "drm-gles2 wayland-gles2"
# Enable encryption support in open62541
PACKAGECONFIG:append:pn-open62541 = " encrypt"
# Enable netperf intervals/pacing and histogram
PACKAGECONFIG:append:pn-netperf = " intervals histogram"
# Use OpenGL ES2 for SDL2
PACKAGECONFIG_GL:pn-libsdl2 = "gles2"
# Add KMS/DRM backend support
PACKAGECONFIG:append:pn-libsdl2 = " kmsdrm"
# Enable cryptodev/devcrypto engine in OpenSSL 1.1
PACKAGECONFIG:append:pn-openssl = " cryptodev-linux"
# Enable GST_TRACER logging in gstreamer
PACKAGECONFIG:append:pn-gstreamer1.0 = " tracer-hooks"
# Enable EGL/GLESv2 support
PACKAGECONFIG:append:pn-cairo = " egl glesv2"